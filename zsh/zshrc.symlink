#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

export ZSH=$HOME/.dotfiles

fpath=($ZSH/zsh/prezto-themes $ZSH/functions $fpath)

if [[ -r $HOME/working/gcloud-zsh-completion/src ]]; then
  fpath=($HOME/working/gcloud-zsh-completion/src $fpath)
fi

typeset -U config_files
setopt extendedglob
config_files=($ZSH/**/*.zsh~$ZSH/zsh/zprezto.symlink/*~$ZSH/vim/vim.symlink/bundle/*~$ZSH/vim/vim.symlink/backups/*)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source $file
done

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source $file
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source $file
done

unset config_files
